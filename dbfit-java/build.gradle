ext {
    fitNesseVersion = '20150424'
    mockitoVersion = '1.10.19'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'com.github.dbfit'
    version = rootProject.dbfitVersion

    checkstyle {
        configFile = new File(parent.projectDir, "config/checkstyle/checkstyle.xml")
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
    }

    // Suppress stricter Java 8 Javadoc checks.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    dependencies {
        compile "org.fitnesse:fitnesse:${fitNesseVersion}:standalone@jar"
    }
}

subprojects {
    jar.baseName = "dbfit-" + project.archivesBaseName
    repositories {
        flatDir {
            dirs "${rootProject.projectDir}/custom_libs"
        }
        mavenCentral()
        maven {
            url "http://mvnrepository.com"
        }
        ivy {
            artifactPattern "https://s3.amazonaws.com/dbfit/[artifact]-[revision].[ext]"
            ivyPattern "https://aws.amazon.com/s3/ivy.xml"
        }
    }

    task libs(type: Copy) {
        from configurations.runtime
        into "$buildDir/libs"
    }

    sourceSets {
        integrationTest {
            java {
                srcDir 'src/integration-test/java'
            }
            resources {
                srcDir 'src/integration-test/resources'
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile "org.hamcrest:java-hamcrest:2.0.0.0"
        testCompile "commons-io:commons-io:2.4"
        testRuntime 'com.dbdeploy:dbdeploy-ant:3.0M3'
        integrationTestCompile "org.fitnesse:fitnesse:${fitNesseVersion}:standalone@jar"
    }

    task integrationTestJar(type: Jar) {
        from sourceSets.integrationTest.output
        baseName = "dbfit-" + project.archivesBaseName + "-integration-test"
        include('**/*Support*')
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        classpath += sourceSets.main.runtimeClasspath
    }

    ext.dbdeploy = { dbDriver, url, userid, password, delimiterType = 'normal' ->
        ant.taskdef(name: 'dbdeploy',
                classname: 'com.dbdeploy.AntTarget',
                classpath: configurations.testRuntime.asPath)
        ant.dbdeploy(driver: dbDriver,
                url: url,
                userid: userid,
                password: password,
                dir: "$projectDir/src/integration-test/resources",
                delimiterType: delimiterType,
        )
    }

    integrationTest.inputs.dir file("$projectDir/../../FitNesseRoot/DbFit/AcceptanceTests/JavaTests")
}


task assembleAll(dependsOn: subprojects.assemble) { }
task libs(dependsOn: subprojects.libs) { }

def publicCoreCompileDeps = [
    'org.reflections:reflections:0.9.9'
  , 'commons-codec:commons-codec:1.10'
  , 'org.apache.commons:commons-lang3:3.4'
]

project('core') {
    description = 'DBFit core api and fixtures'

    dependencies {
        compile "dummy:fitlibrary:20091020@jar"
        compile publicCoreCompileDeps
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }
}

project('db2') {
    description = 'DBFit db2 engine'

    dependencies {
        compile project(':dbfit-java:core')
        runtime 'com.ibm.db2:db2jcc4:4.17.29@jar'
    }
}

project('derby') {
    description = 'DBFit derby engine'
    configurations {
        derby
        testRuntime.extendsFrom derby
        integrationTestRuntime.extendsFrom derby
    }
    dependencies {
        compile project(':dbfit-java:core')
        derby group: 'org.apache.derby', name: 'derby', version: '10.10.1.1'
        derby group: 'org.apache.derby', name: 'derbyclient', version: '10.10.1.1'
    }
}

def publicHsqldbRuntimeDeps = [
    'org.hsqldb:hsqldb:2.3.2@jar'
]

project('hsqldb') {
    description = 'DBFit HSQLDB engine'

    dependencies {
        compile project(':dbfit-java:core')
        runtime publicHsqldbRuntimeDeps
    }
}

def publicOracleCompileDeps = [
    'commons-io:commons-io:2.4'
]

project('oracle') {
    description = 'DBFit Oracle engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile 'dummy:ojdbc6:11.2.0.3.0@jar'
        compile publicOracleCompileDeps
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    integrationTest.doFirst {
        copy {
            from 'TestDbConnectionDbFitOracle.properties.default'
            into '.'
            rename '(.*).default', '$1'
        }

        copy {
            from 'TestDbConnectionDbFitOracle.properties.custom'
            into '.'
            rename '(.*).custom', '$1'
        }
    }
}

def publicMysqlCompileDeps = [
    'mysql:mysql-connector-java:5.1.6'
]

project('mysql') {
    description = 'DBFit MySQL engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile publicMysqlCompileDeps
    }

    integrationTest.doFirst {
        dbdeploy('com.mysql.jdbc.Driver', 'jdbc:mysql://127.0.0.1/dbfit', 'dbfit_user', 'password')
    }
}

def publicPostgresCompileDeps = [
    'org.postgresql:postgresql:9.3-1101-jdbc41'
]

project('postgres') {
    description = 'DBFit PostGreSQL engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile publicPostgresCompileDeps
    }

    integrationTest.doFirst {
        dbdeploy('org.postgresql.Driver', 'jdbc:postgresql:dbfit', 'dbfit', 'dbfit', 'row')
    }
}

project('sqlserver') {
    description = 'DBFit sqlserver engine'

    dependencies {
        compile project(':dbfit-java:core')
        runtime 'com.microsoft:sqljdbc:4.1.5605@jar'
        integrationTestRuntime 'com.microsoft:sqljdbc:4.1.5605@jar'
    }
}

project('teradata') {
    description = 'DBFit Teradata engine'

    dependencies {
        compile project(':dbfit-java:core')
        runtime 'com.teradata:terajdbc4:15.0.0.15@jar'
        runtime 'com.teradata:tdgssconfig:15.0.0.15@jar'
    }
}

project('netezza') {
    description = 'DBFit netezza engine'

    dependencies {
        compile project(':dbfit-java:core')
        runtime 'org.netezza:nzjdbc:7.1@jar'
    }
}

// Aggregated dependencies to include in the POM file when publishing.
dependencies {
    compile publicCoreCompileDeps
    runtime publicHsqldbRuntimeDeps
    compile publicOracleCompileDeps
    compile publicMysqlCompileDeps
    compile publicPostgresCompileDeps
}

// Remove the default artifact.
configurations.archives.artifacts.with { archives ->
    def jarArtifact
    archives.each {
        if (it.file =~ 'jar') {
            jarArtifact = it
        }
    }
    remove(jarArtifact)
}

task allJavadoc(type: Javadoc) {
    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    destinationDir = new File(buildDir, 'doc/javadoc')
    classpath = files(subprojects.collect { project ->
                          project.sourceSets.main.compileClasspath
                      }
                     )
}

task allJavadocJar(type: Jar) {
    baseName = 'dbfit'
    classifier = 'javadoc'
    from allJavadoc
}

task allSourcesJar(type: Jar) {
    baseName = 'dbfit'
    classifier = 'sources'
    subprojects.each { subproject ->
        from subproject.sourceSets.main.allSource
    }
}

task allJar(type: Jar, dependsOn: subprojects.build) {
    baseName = 'dbfit'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

artifacts {
    archives allJar, allJavadocJar, allSourcesJar
}

signing {
    required { rootProject.isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DbFit'
                packaging 'jar'
                description 'Database test automation for FitNesse'

                url 'https://github.com/dbfit/dbfit'
                scm {
                    connection 'scm:git@github.com/dbfit/dbfit.git'
                    developerConnection 'scm:git@github.com/dbfit/dbfit.git'
                    url 'https://github.com/dbfit/dbfit'
                }

                licenses {
                    license {
                        name 'GNU General Public License, version 2'
                        url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'benilovj'
                        name 'Jake Benilov'
                        email 'benilov@gmail.com'
                    }
                    developer {
                        id 'javornikolov'
                        name 'Yavor Nikolov'
                        email 'nikolov.javor@gmail.com'
                    }
                }
            }
        }
     }
}
